package Parse;

import java_cup.runtime.*;

/* Java 1.5 parser for CUP.
 * (actually "Java 1.4 with JSR-14" parser,
 *  since Java 1.5 has not yet been finalized)
 * Copyright (C) 2002 C. Scott Ananian <cananian@alumni.princeton.edu>
 * This program is released under the terms of the GPL; see the file
 * COPYING for more details.  There is NO WARRANTY on this code.
 */

/*
JDK 1.5 Features added:
  parameterized types.  a "smart lexer" is necessary to fit this into
  an LALR(1) grammar.
*/
parser code  {: 
  Lexer lexer;

  public  Grm15(Lexer l) {
    super();
    lexer=l;
  }

  public void syntax_error(java_cup.runtime.Symbol current) {
    report_error("Syntax error (" + current.sym + ")", current);
  }
  public void report_error(String message, java_cup.runtime.Symbol info) {
    lexer.errorMsg(message, info);
  }
:};

scan with {: return lexer.nextToken(); :};

terminal BOOLEAN; // primitive_type
terminal BYTE, SHORT, INT, LONG, CHAR; // integral_type
terminal FLOAT, DOUBLE; // floating_point_type
terminal LBRACK, RBRACK; // array_type
terminal IDENTIFIER; // name
terminal DOT; // qualified_name
terminal SEMICOLON, MULT, COMMA, LBRACE, RBRACE, EQ, LPAREN, RPAREN, COLON;
terminal PACKAGE; // package_declaration
terminal IMPORT; // import_declaration
terminal PUBLIC, PROTECTED, PRIVATE; // modifier
terminal STATIC; // modifier
terminal ABSTRACT, FINAL, NATIVE, SYNCHRONIZED, TRANSIENT, VOLATILE;
terminal CLASS; // class_declaration
terminal EXTENDS; // super
terminal IMPLEMENTS; // interfaces
terminal VOID; // method_header
terminal THROWS; // throws
terminal THIS, SUPER; // explicit_constructor_invocation
terminal INTERFACE; // interface_declaration
terminal IF, ELSE; // if_then_statement, if_then_else_statement
terminal SWITCH; // switch_statement
terminal CASE, DEFAULT; // switch_label
terminal DO, WHILE; // while_statement, do_statement
terminal FOR; // for_statement
terminal BREAK; // break_statement
terminal CONTINUE; // continue_statement
terminal RETURN; // return_statement
terminal THROW; // throw_statement
terminal TRY; // try_statement
terminal CATCH; // catch_clause
terminal FINALLY; // finally
terminal NEW; // class_instance_creation_expression
terminal PLUSPLUS; // postincrement_expression
terminal MINUSMINUS; // postdecrement_expression
terminal PLUS, MINUS, COMP, NOT, DIV, MOD;
terminal LSHIFT, RSHIFT, URSHIFT; // shift_expression
terminal LT, GT, LTEQ, GTEQ, INSTANCEOF; // relational_expression
terminal EQEQ, NOTEQ; // equality_expression
terminal AND; // and_expression
terminal XOR; // exclusive_or_expression
terminal OR;  // inclusive_or_expression
terminal ANDAND; // conditional_and_expression
terminal OROR; // conditional_or_expression
terminal QUESTION; // conditional_expression
terminal MULTEQ, DIVEQ, MODEQ, PLUSEQ, MINUSEQ; // assignment_operator
terminal LSHIFTEQ, RSHIFTEQ, URSHIFTEQ; // assignment_operator
terminal ANDEQ, XOREQ, OREQ; // assignment_operator

terminal java.lang.Number INTEGER_LITERAL;
terminal java.lang.Number FLOATING_POINT_LITERAL;
terminal java.lang.Boolean BOOLEAN_LITERAL;
terminal java.lang.Character CHARACTER_LITERAL;
terminal java.lang.String STRING_LITERAL;
terminal NULL_LITERAL;

// Reserved but unused:
terminal CONST, GOTO;
// strictfp keyword, new in Java 1.2
terminal STRICTFP;
// assert keyword, new in Java 1.4
terminal ASSERT; // assert_statement
// 'start of a parameterized type' terminal, required for LALR(1) parsing
// of casts to parameterized types. it's an "LT with lookahead".
terminal PLT;

// 19.2) The Syntactic Grammar
non terminal goal;
// 19.3) Lexical Structure
non terminal literal;
// 19.4) Types, Values, and Variables
non terminal type, primitive_type, numeric_type;
non terminal integral_type, floating_point_type;
non terminal reference_type;
non terminal class_or_interface_type;
non terminal class_type, interface_type;
non terminal array_type;
// 19.5) Names
non terminal name, simple_name, qualified_name;
// 19.6) Packages
non terminal compilation_unit;
non terminal package_declaration_opt, package_declaration;
non terminal import_declarations_opt, import_declarations;
non terminal type_declarations_opt, type_declarations;
non terminal import_declaration;
non terminal single_type_import_declaration;
non terminal type_import_on_demand_declaration;
non terminal type_declaration;
// 19.7) Productions used only in the LALR(1) grammar
non terminal modifiers_opt, modifiers, modifier;
// 19.8.1) Class Declaration
non terminal class_declaration, super, super_opt;
non terminal interfaces, interfaces_opt, interface_type_list;
non terminal class_body;
non terminal class_body_declarations, class_body_declarations_opt;
non terminal class_body_declaration, class_member_declaration;
// 19.8.2) Field Declarations
non terminal field_declaration, variable_declarators, variable_declarator;
non terminal variable_declarator_id, variable_initializer;
// 19.8.3) Method Declarations
non terminal method_declaration, method_header, method_declarator;
non terminal formal_parameter_list_opt, formal_parameter_list;
non terminal formal_parameter;
non terminal throws_opt, throws;
non terminal class_type_list, method_body;
// 19.8.4) Static Initializers
non terminal static_initializer;
// 19.8.5) Constructor Declarations
non terminal constructor_declaration, constructor_declarator;
non terminal constructor_body;
non terminal explicit_constructor_invocation;
// 19.9.1) Interface Declarations
non terminal interface_declaration;
non terminal extends_interfaces_opt, extends_interfaces;
non terminal interface_body;
non terminal interface_member_declarations_opt, interface_member_declarations;
non terminal interface_member_declaration, constant_declaration;
non terminal abstract_method_declaration;
// 19.10) Arrays
non terminal array_initializer;
non terminal variable_initializers;
// 19.11) Blocks and Statements
non terminal block;
non terminal block_statements_opt, block_statements, block_statement;
non terminal local_variable_declaration_statement, local_variable_declaration;
non terminal statement, statement_no_short_if;
non terminal statement_without_trailing_substatement;
non terminal empty_statement;
non terminal labeled_statement, labeled_statement_no_short_if;
non terminal expression_statement, statement_expression;
non terminal if_then_statement;
non terminal if_then_else_statement, if_then_else_statement_no_short_if;
non terminal switch_statement, switch_block;
non terminal switch_block_statement_groups;
non terminal switch_block_statement_group;
non terminal switch_labels, switch_label;
non terminal while_statement, while_statement_no_short_if;
non terminal do_statement;
non terminal for_statement, for_statement_no_short_if;
non terminal for_init_opt, for_init;
non terminal for_update_opt, for_update;
non terminal statement_expression_list;
non terminal identifier_opt;
non terminal break_statement, continue_statement;
non terminal return_statement, throw_statement;
non terminal synchronized_statement, try_statement;
non terminal catches_opt, catches, catch_clause;
non terminal finally;
non terminal assert_statement;
// 19.12) Expressions
non terminal primary, primary_no_new_array;
non terminal class_instance_creation_expression;
non terminal argument_list_opt, argument_list;
non terminal array_creation_expression;
non terminal dim_exprs, dim_expr, dims_opt, dims;
non terminal field_access, method_invocation, array_access;
non terminal postfix_expression;
non terminal postincrement_expression, postdecrement_expression;
non terminal unary_expression, unary_expression_not_plus_minus;
non terminal preincrement_expression, predecrement_expression;
non terminal cast_expression;
non terminal multiplicative_expression, additive_expression;
non terminal shift_expression, relational_expression, equality_expression;
non terminal and_expression, exclusive_or_expression, inclusive_or_expression;
non terminal conditional_and_expression, conditional_or_expression;
non terminal conditional_expression, assignment_expression;
non terminal assignment;
non terminal assignment_operator;
non terminal expression;
// JSR-14 2.1) Type Syntax 2.3) Handling Consecutive Type Brackets
non terminal type_variable;
non terminal type_arguments, type_arguments_opt;
non terminal reference_type_list;
non terminal reference_type_list_1, reference_type_1;
non terminal reference_type_list_2, reference_type_2;
non terminal reference_type_list_3, reference_type_3;
// JSR-14 2.2) Parameterized Type Declarations 2.3) Handling Consecutive...
non terminal type_parameters, type_parameters_opt;
non terminal type_parameter, type_parameter_list;
non terminal type_parameter_1, type_parameter_list_1;
non terminal type_bound, type_bound_opt;
non terminal type_bound_1;
non terminal additional_bound_list, additional_bound_list_opt;
non terminal additional_bound_list_1;
non terminal additional_bound;
non terminal additional_bound_1;
// not mentioned in the JSR: need to reduce the precedence of instanceof
non terminal instanceof_expression;
//// expressions which are Not a Name
non terminal postfix_expression_nn;
non terminal unary_expression_nn;
non terminal unary_expression_not_plus_minus_nn;
non terminal multiplicative_expression_nn;
non terminal additive_expression_nn;
non terminal shift_expression_nn;
non terminal relational_expression_nn;
non terminal instanceof_expression_nn;
non terminal equality_expression_nn;
non terminal and_expression_nn;
non terminal exclusive_or_expression_nn;
non terminal inclusive_or_expression_nn;
non terminal conditional_and_expression_nn;
non terminal conditional_or_expression_nn;
non terminal conditional_expression_nn;
non terminal assignment_expression_nn;
non terminal expression_nn, expression_nn_opt, constant_expression_nn;
//// expressions where LT may be mistaken for PLT
non terminal unary_expression_l;
non terminal preincrement_expression_l, predecrement_expression_l;
non terminal unary_expression_not_plus_minus_l;
non terminal cast_expression_l;
non terminal multiplicative_expression_l;
non terminal additive_expression_l;
non terminal shift_expression_l;
non terminal relational_expression_l;
non terminal instanceof_expression_l;
non terminal equality_expression_l;
non terminal and_expression_l;
non terminal exclusive_or_expression_l;
non terminal inclusive_or_expression_l;
non terminal conditional_and_expression_l;
non terminal conditional_or_expression_l;
non terminal conditional_expression_l;
non terminal assignment_expression_l, assignment_l;
non terminal expression_l;
non terminal statement_expression_l;

start with goal;

// 19.2) The Syntactic Grammar
goal ::=	compilation_unit
	;

// 19.3) Lexical Structure.
literal ::=	INTEGER_LITERAL
	|	FLOATING_POINT_LITERAL
	|	BOOLEAN_LITERAL
	|	CHARACTER_LITERAL
	|	STRING_LITERAL
	|	NULL_LITERAL
	;

// 19.4) Types, Values, and Variables
type	::=	primitive_type
	|	reference_type
	;
primitive_type ::=
		numeric_type
	|	BOOLEAN
	;
numeric_type::=	integral_type
	|	floating_point_type
	;
integral_type ::= 
		BYTE 
	|	SHORT 
	|	INT 
	|	LONG 
	|	CHAR 
	;
floating_point_type ::= 
		FLOAT 
	|	DOUBLE
	;

reference_type ::=
		class_or_interface_type
/* note that the 'type_variable' production will come out of the grammar
 * as a 'class_or_interface_type' with a 'simple_name'.  The semantic
 * checker will have to resolve whether this is a class name or a type
 * variable */
	|	array_type
	;
type_variable ::=
		IDENTIFIER
	;
class_or_interface_type ::=
		name type_arguments_opt
	;

class_type ::=	class_or_interface_type;
interface_type ::= class_or_interface_type;		

array_type ::=	primitive_type dims
	|	name dims
	|	name type_arguments dims
	;

type_arguments_opt ::= type_arguments | ;
type_arguments ::=
		PLT reference_type_list_1
	;
reference_type_list ::=
		reference_type
	|	reference_type_list COMMA reference_type
	;
reference_type_list_1 ::=
		reference_type_1
	|	reference_type_list COMMA reference_type_1
	;
reference_type_list_2 ::=
		reference_type_2
	|	reference_type_list COMMA reference_type_2
	;
reference_type_list_3 ::=
		reference_type_3
	|	reference_type_list COMMA reference_type_3
	;
// note the LT in place of the PLT, since the following RSHIFT will
// confuse the lexer lookahead.
reference_type_1 ::=
		reference_type GT
	|	name LT reference_type_list_2
	|	name PLT reference_type_list_2
	;
reference_type_2 ::=
		reference_type RSHIFT
	|	name LT reference_type_list_3
	|	name PLT reference_type_list_3
	;
reference_type_3 ::=
		reference_type URSHIFT
	;

// 19.5) Names
name	::=	simple_name
	|	qualified_name
	;
simple_name ::=	IDENTIFIER
	;
qualified_name ::=
		name DOT IDENTIFIER
	;

// 19.6) Packages
compilation_unit ::=
		package_declaration_opt 
		import_declarations_opt
		type_declarations_opt
		;
package_declaration_opt ::= package_declaration | ;
import_declarations_opt ::= import_declarations | ;
type_declarations_opt   ::= type_declarations   | ;

import_declarations ::= 
		import_declaration
	|	import_declarations import_declaration
	;
type_declarations ::= 
		type_declaration
	|	type_declarations type_declaration
	;
package_declaration ::= 
		PACKAGE name SEMICOLON
	;
import_declaration ::= 
		single_type_import_declaration
	|	type_import_on_demand_declaration
	;
single_type_import_declaration ::= 
		IMPORT name SEMICOLON
	;
type_import_on_demand_declaration ::=
		IMPORT name DOT MULT SEMICOLON
	;
type_declaration ::=
		class_declaration
	|	interface_declaration
	|	SEMICOLON
	;

// 19.7) Productions used only in the LALR(1) grammar
modifiers_opt::=
	|	modifiers
	;
modifiers ::= 	modifier
	|	modifiers modifier
	;
modifier ::=	PUBLIC | PROTECTED | PRIVATE
	|	STATIC
	|	ABSTRACT | FINAL | NATIVE | SYNCHRONIZED | TRANSIENT | VOLATILE
	|	STRICTFP // note that semantic analysis must check that the
	                 // context of the modifier allows strictfp.
	;

// 19.8) Classes

// 19.8.1) Class Declaration:
class_declaration ::= 
	modifiers_opt CLASS IDENTIFIER type_parameters_opt
	  super_opt interfaces_opt class_body
	;
super ::=	EXTENDS class_type
	;
super_opt ::=	
	|	super
	;
interfaces ::=	IMPLEMENTS interface_type_list
	;
interfaces_opt::=
	|	interfaces 
	;
interface_type_list ::= 
		interface_type
	|	interface_type_list COMMA interface_type
	;
class_body ::=	LBRACE class_body_declarations_opt RBRACE 
	;
class_body_declarations_opt ::= 
	|	class_body_declarations ;
class_body_declarations ::= 
		class_body_declaration
	|	class_body_declarations class_body_declaration
	;
class_body_declaration ::=
		class_member_declaration
	|	static_initializer
	|	constructor_declaration
	|	block
	;
class_member_declaration ::=
		field_declaration
	|	method_declaration
	/* repeat the prod for 'class_declaration' here: */
	|	modifiers_opt CLASS IDENTIFIER type_parameters_opt super_opt interfaces_opt class_body
	|	interface_declaration
	|	SEMICOLON
	;

// 19.8.2) Field Declarations
field_declaration ::= 
		modifiers_opt type variable_declarators SEMICOLON
	;
variable_declarators ::=
		variable_declarator
	|	variable_declarators COMMA variable_declarator
	;
variable_declarator ::=
		variable_declarator_id
	|	variable_declarator_id EQ variable_initializer
	;
variable_declarator_id ::=
		IDENTIFIER
	|	variable_declarator_id LBRACK RBRACK
	;
variable_initializer ::=
		expression_l
	|	array_initializer
	;

// 19.8.3) Method Declarations
method_declaration ::=
		method_header method_body
	;
method_header ::=
	// have to expand type_parameters_opt here so that we don't
	// force an early decision of whether this is a field_declaration
	// or a method_declaration (the type_parameters_opt would have to
	// be reduced when we see the 'type' if this was a method declaration,
	// but it might still turn out to be a field declaration).
	// also, we can't rely on the special PLT token here, as the LT
	// doesn't follow a IDENTIFIER
		modifiers_opt type method_declarator throws_opt
	|	modifiers_opt LT type_parameter_list_1 type method_declarator throws_opt
	|	modifiers_opt VOID method_declarator throws_opt
	|	modifiers_opt LT type_parameter_list_1 VOID method_declarator throws_opt
	;
method_declarator ::=
		IDENTIFIER LPAREN formal_parameter_list_opt RPAREN
	|	method_declarator LBRACK RBRACK // deprecated
	// be careful; the above production also allows 'void foo() []'
	;
formal_parameter_list_opt ::=
	|	formal_parameter_list
	;
formal_parameter_list ::=
		formal_parameter
	|	formal_parameter_list COMMA formal_parameter
	;
formal_parameter ::=
		type variable_declarator_id
	|	FINAL type variable_declarator_id
	;
throws_opt ::=	
	|	throws
	;
throws ::=	THROWS class_type_list
	;
class_type_list ::=
		class_type
	|	class_type_list COMMA class_type
	;
method_body ::=	block
	|	SEMICOLON
	;

// 19.8.4) Static Initializers
static_initializer ::=
		STATIC block
	;

// 19.8.5) Constructor Declarations
constructor_declaration ::=
		modifiers_opt constructor_declarator
			throws_opt constructor_body
	|	modifiers_opt LT type_parameter_list_1 constructor_declarator
			throws_opt constructor_body
	;
constructor_declarator ::=
		simple_name LPAREN formal_parameter_list_opt RPAREN
	;
constructor_body ::=
		LBRACE explicit_constructor_invocation
			block_statements RBRACE
	|	LBRACE explicit_constructor_invocation RBRACE
	|	LBRACE block_statements RBRACE
	|	LBRACE RBRACE
	;
explicit_constructor_invocation ::=
		THIS LPAREN argument_list_opt RPAREN SEMICOLON
	|	SUPER LPAREN argument_list_opt RPAREN SEMICOLON
	|	primary DOT THIS LPAREN argument_list_opt RPAREN SEMICOLON
	|	primary DOT SUPER LPAREN argument_list_opt RPAREN SEMICOLON
	;

// 19.9) Interfaces

// 19.9.1) Interface Declarations
interface_declaration ::=
		modifiers_opt INTERFACE IDENTIFIER type_parameters_opt
		  extends_interfaces_opt interface_body
	;
extends_interfaces_opt ::=
	|	extends_interfaces
	;
extends_interfaces ::=
		EXTENDS interface_type
	|	extends_interfaces COMMA interface_type
	;
interface_body ::=
		LBRACE interface_member_declarations_opt RBRACE
	;
interface_member_declarations_opt ::=
	|	interface_member_declarations
	;
interface_member_declarations ::=
		interface_member_declaration
	|	interface_member_declarations interface_member_declaration
	;
interface_member_declaration ::=
		constant_declaration
	|	abstract_method_declaration
	|	class_declaration
	|	interface_declaration
	;
constant_declaration ::=
		field_declaration
	// need to semantically check that modifiers of field declaration
	// include only PUBIC, STATIC, or FINAL.  Other modifiers are
	// disallowed.
	;
abstract_method_declaration ::=
		method_header SEMICOLON
	;

// 19.10) Arrays
array_initializer ::=
		LBRACE variable_initializers COMMA RBRACE
	|	LBRACE variable_initializers RBRACE
	|	LBRACE COMMA RBRACE
	|	LBRACE RBRACE
	;
variable_initializers ::=
		variable_initializer
	|	variable_initializers COMMA variable_initializer
	;

// 19.11) Blocks and Statements
block ::=	LBRACE block_statements_opt RBRACE
	;
block_statements_opt ::=
	|	block_statements
	;
block_statements ::=
		block_statement
	|	block_statements block_statement
	;
block_statement ::=
		local_variable_declaration_statement
	|	statement
	|	class_declaration
	|	interface_declaration
	;
local_variable_declaration_statement ::=
		local_variable_declaration SEMICOLON
	;
local_variable_declaration ::=
		type variable_declarators
	|	FINAL type variable_declarators
	;
statement ::=	statement_without_trailing_substatement
	|	labeled_statement
	|	if_then_statement
	|	if_then_else_statement
	|	while_statement
	|	for_statement
	;
statement_no_short_if ::=
		statement_without_trailing_substatement
	|	labeled_statement_no_short_if
	|	if_then_else_statement_no_short_if
	|	while_statement_no_short_if
	|	for_statement_no_short_if
	;
statement_without_trailing_substatement ::=
		block
	|	empty_statement
	|	expression_statement
	|	switch_statement
	|	do_statement
	|	break_statement
	|	continue_statement
	|	return_statement
	|	synchronized_statement
	|	throw_statement
	|	try_statement
	|	assert_statement
	;
empty_statement ::=
		SEMICOLON
	;
labeled_statement ::=
		IDENTIFIER COLON statement
	;
labeled_statement_no_short_if ::=
		IDENTIFIER COLON statement_no_short_if
	;
expression_statement ::=
		statement_expression SEMICOLON
	;
statement_expression ::=
		assignment
	|	preincrement_expression
	|	predecrement_expression
	|	postincrement_expression
	|	postdecrement_expression
	|	method_invocation
	|	class_instance_creation_expression
	;
if_then_statement ::=
		IF LPAREN expression_nn RPAREN statement
	|	IF LPAREN name RPAREN statement
	;
if_then_else_statement ::=
		IF LPAREN expression_nn RPAREN statement_no_short_if 
			ELSE statement
	|	IF LPAREN name RPAREN statement_no_short_if 
			ELSE statement
	;
if_then_else_statement_no_short_if ::=
		IF LPAREN expression_nn RPAREN statement_no_short_if
			ELSE statement_no_short_if
	|	IF LPAREN name RPAREN statement_no_short_if
			ELSE statement_no_short_if
	;
switch_statement ::=
		SWITCH LPAREN expression_nn RPAREN switch_block
	|	SWITCH LPAREN name RPAREN switch_block
	;
switch_block ::=
		LBRACE switch_block_statement_groups switch_labels RBRACE
	|	LBRACE switch_block_statement_groups RBRACE
	|	LBRACE switch_labels RBRACE
	|	LBRACE RBRACE
	;
switch_block_statement_groups ::=
		switch_block_statement_group
	|	switch_block_statement_groups switch_block_statement_group
	;
switch_block_statement_group ::=
		switch_labels block_statements
	;
switch_labels ::=
		switch_label
	|	switch_labels switch_label
	;
switch_label ::=
		CASE constant_expression_nn COLON
	|	CASE name COLON
	|	DEFAULT COLON
	;

while_statement ::=
		WHILE LPAREN expression_nn RPAREN statement
	|	WHILE LPAREN name RPAREN statement
	;
while_statement_no_short_if ::=
		WHILE LPAREN expression_nn RPAREN statement_no_short_if
	|	WHILE LPAREN name RPAREN statement_no_short_if
	;
do_statement ::=
		DO statement WHILE LPAREN expression_nn RPAREN SEMICOLON
	|	DO statement WHILE LPAREN name RPAREN SEMICOLON
	;
for_statement ::=
		FOR LPAREN for_init_opt SEMICOLON expression_nn_opt SEMICOLON
			for_update_opt RPAREN statement
	|	FOR LPAREN for_init_opt SEMICOLON name SEMICOLON
			for_update_opt RPAREN statement
	;
for_statement_no_short_if ::=
		FOR LPAREN for_init_opt SEMICOLON expression_nn_opt SEMICOLON
			for_update_opt RPAREN statement_no_short_if
	|	FOR LPAREN for_init_opt SEMICOLON name SEMICOLON
			for_update_opt RPAREN statement_no_short_if
	;
for_init_opt ::=
	|	for_init
	;
for_init ::=	statement_expression_list
	|	local_variable_declaration
	;
for_update_opt ::=
	|	for_update
	;
for_update ::=	statement_expression_list
	;
statement_expression_list ::=
		statement_expression_l
	|	statement_expression_list COMMA statement_expression_l
	;

identifier_opt ::= 
	|	IDENTIFIER
	;

break_statement ::=
		BREAK identifier_opt SEMICOLON
	;

continue_statement ::=
		CONTINUE identifier_opt SEMICOLON
	;
return_statement ::=
		RETURN expression_nn_opt SEMICOLON
	|	RETURN name SEMICOLON
	;
throw_statement ::=
		THROW expression_nn SEMICOLON
	|	THROW name SEMICOLON
	;
synchronized_statement ::=
		SYNCHRONIZED LPAREN expression_nn RPAREN block
	|	SYNCHRONIZED LPAREN name RPAREN block
	;
try_statement ::=
		TRY block catches
	|	TRY block catches_opt finally
	;
catches_opt ::=
	|	catches
	;
catches ::=	catch_clause
	|	catches catch_clause
	;
catch_clause ::=
		CATCH LPAREN formal_parameter RPAREN block
	;
finally ::=	FINALLY block
	;
assert_statement ::=
		ASSERT expression_nn SEMICOLON
	|	ASSERT expression_nn COLON expression_nn SEMICOLON
	|	ASSERT name SEMICOLON
	|	ASSERT name COLON expression_nn SEMICOLON
	|	ASSERT expression_nn COLON name SEMICOLON
	|	ASSERT name COLON name SEMICOLON
	;

// 19.12) Expressions
primary ::=	primary_no_new_array
	|	array_creation_expression
	;
primary_no_new_array ::=
		literal
	|	THIS
	|	LPAREN name RPAREN
	|	LPAREN expression_nn RPAREN
	|	class_instance_creation_expression
	|	field_access
	|	method_invocation
	|	array_access
	|	name DOT THIS
	|	VOID DOT CLASS
	// "Type DOT CLASS", but expanded
	|	primitive_type DOT CLASS
	|	name DOT CLASS
// this production *might* be valid, but introduces conflicts.
//	|	name type_arguments DOT CLASS
	|	primitive_type dims DOT CLASS
	|	name dims DOT CLASS
// this production *might* be valid, but introduces conflicts.
//	|	name type_arguments dims DOT CLASS
	;
class_instance_creation_expression ::=
	// actually only class_type is valid in the next production, but
	// that would lead to a grammar conflict.
		NEW class_or_interface_type LPAREN argument_list_opt RPAREN
	|	NEW class_or_interface_type LPAREN argument_list_opt RPAREN class_body
	|	primary DOT NEW IDENTIFIER type_arguments_opt
			LPAREN argument_list_opt RPAREN
	|	primary DOT NEW IDENTIFIER type_arguments_opt
			LPAREN argument_list_opt RPAREN class_body
	;
argument_list_opt ::=
	|	argument_list
	;
argument_list ::=
		expression_l
	|	argument_list COMMA expression
	;
array_creation_expression ::=
		NEW primitive_type dim_exprs dims_opt
	|	NEW class_or_interface_type dim_exprs dims_opt
	|	NEW primitive_type dims array_initializer
	|	NEW class_or_interface_type dims array_initializer
	;
dim_exprs ::=	dim_expr
	|	dim_exprs dim_expr
	;
dim_expr ::=	LBRACK expression RBRACK
	;
dims_opt ::=
	|	dims
	;
dims ::=	LBRACK RBRACK
	|	dims LBRACK RBRACK
	;
field_access ::=
		primary DOT IDENTIFIER
	|	SUPER DOT IDENTIFIER
	|	name DOT SUPER DOT IDENTIFIER
	;
method_invocation ::=
		name LPAREN argument_list_opt RPAREN
	|	primary DOT IDENTIFIER LPAREN argument_list_opt RPAREN
	|	SUPER DOT IDENTIFIER LPAREN argument_list_opt RPAREN
	|	name DOT SUPER DOT IDENTIFIER LPAREN argument_list_opt RPAREN
	;
array_access ::=
		name LBRACK expression RBRACK
	|	primary_no_new_array LBRACK expression RBRACK
	;
postfix_expression ::=
		primary
	|	name
	|	postincrement_expression
	|	postdecrement_expression
	;
postincrement_expression ::=
		postfix_expression PLUSPLUS
	;
postdecrement_expression ::=
		postfix_expression MINUSMINUS
	;
unary_expression ::=
		preincrement_expression
	|	predecrement_expression
	|	PLUS unary_expression
	|	MINUS unary_expression
	|	unary_expression_not_plus_minus
	;
preincrement_expression ::=
		PLUSPLUS unary_expression
	;
predecrement_expression ::=
		MINUSMINUS unary_expression
	;
unary_expression_not_plus_minus ::=
		postfix_expression
	|	COMP unary_expression
	|	NOT unary_expression
	|	cast_expression
	;
cast_expression ::=
		LPAREN primitive_type dims_opt RPAREN unary_expression
	|	LPAREN name RPAREN unary_expression_not_plus_minus
	|	LPAREN name PLT reference_type_list_1 RPAREN unary_expression_not_plus_minus
	|	LPAREN name dims RPAREN unary_expression_not_plus_minus
	|	LPAREN name PLT reference_type_list_1 dims RPAREN unary_expression_not_plus_minus
	;
multiplicative_expression ::=
		unary_expression
	|	multiplicative_expression MULT unary_expression
	|	multiplicative_expression DIV unary_expression
	|	multiplicative_expression MOD unary_expression
	;
additive_expression ::=
		multiplicative_expression
	|	additive_expression PLUS multiplicative_expression
	|	additive_expression MINUS multiplicative_expression
	;
shift_expression ::=
		additive_expression
	|	shift_expression LSHIFT additive_expression
	|	shift_expression RSHIFT additive_expression
	|	shift_expression URSHIFT additive_expression
	;
relational_expression ::=
		shift_expression
	|	relational_expression LT shift_expression
	|	relational_expression GT shift_expression
	|	relational_expression LTEQ shift_expression
	|	relational_expression GTEQ shift_expression
	;
instanceof_expression ::=
		relational_expression
	|	instanceof_expression INSTANCEOF reference_type
	;
equality_expression ::=
		instanceof_expression
	|	equality_expression EQEQ instanceof_expression
	|	equality_expression NOTEQ instanceof_expression
	;
and_expression ::=
		equality_expression
	|	and_expression AND equality_expression
	;
exclusive_or_expression ::=
		and_expression
	|	exclusive_or_expression XOR and_expression
	;
inclusive_or_expression ::=
		exclusive_or_expression
	|	inclusive_or_expression OR exclusive_or_expression
	;
conditional_and_expression ::=
		inclusive_or_expression
	|	conditional_and_expression ANDAND inclusive_or_expression
	;
conditional_or_expression ::=
		conditional_and_expression
	|	conditional_or_expression OROR conditional_and_expression
	;
conditional_expression ::=
		conditional_or_expression
	|	conditional_or_expression QUESTION expression 
			COLON conditional_expression
	;
assignment_expression ::=
		conditional_expression
	|	assignment
	;
assignment ::=
		name         assignment_operator assignment_expression
	|	field_access assignment_operator assignment_expression
	|	array_access assignment_operator assignment_expression
	;
assignment_operator ::=
		EQ
	|	MULTEQ
	|	DIVEQ
	|	MODEQ
	|	PLUSEQ
	|	MINUSEQ
	|	LSHIFTEQ
	|	RSHIFTEQ
	|	URSHIFTEQ
	|	ANDEQ
	|	XOREQ
	|	OREQ
	;
expression ::=	assignment_expression
	;

// JLS-14 productions.
type_parameters_opt ::= type_parameters | ;
type_parameters ::=
		PLT type_parameter_list_1
	;
type_parameter_list ::=
		type_parameter_list COMMA type_parameter
	|	type_parameter
	;
type_parameter_list_1 ::=
		type_parameter_1
	|	type_parameter_list COMMA type_parameter_1
	;
type_parameter ::=
		type_variable type_bound_opt
	;
type_parameter_1 ::=
		type_variable GT
	|	type_variable type_bound_1
// I don't think this production is really meant to be used.
//	|	type_variable IMPLEMENTS reference_type_1
	;
type_bound_opt ::= type_bound | ;
type_bound ::=
		EXTENDS reference_type additional_bound_list_opt
	;
type_bound_1 ::=
		EXTENDS reference_type_1
	|	EXTENDS reference_type additional_bound_list_1
	;
additional_bound_list_opt ::= additional_bound_list | ;
additional_bound_list ::=
		additional_bound additional_bound_list
	|	additional_bound
	;
additional_bound_list_1 ::=
		additional_bound additional_bound_list_1
	|	additional_bound_1
	;
additional_bound ::=
		AND interface_type
	;
additional_bound_1 ::=
		AND reference_type_1
	;
//////////////////////////////////////////////
// the following productions are copied from the standard ones, but
// 'name' all alone is not allowed.  The '_nn' stands for 'not name'.
// this allows us to postpone the decision whether '(x)' is an expression
// or a type-cast until we can see enough right context to make the proper
// choice.
postfix_expression_nn ::=
		primary
	// the 'name' production was removed here.
	|	postincrement_expression
	|	postdecrement_expression
	;
unary_expression_nn ::=
		preincrement_expression
	|	predecrement_expression
	|	PLUS unary_expression
	|	MINUS unary_expression
	|	unary_expression_not_plus_minus_nn
	;
unary_expression_not_plus_minus_nn ::=
		postfix_expression_nn
	|	COMP unary_expression
	|	NOT unary_expression
	|	cast_expression
	;
multiplicative_expression_nn ::=
		unary_expression_nn
	|	multiplicative_expression MULT unary_expression
	|	multiplicative_expression DIV unary_expression
	|	multiplicative_expression MOD unary_expression
	;
additive_expression_nn ::=
		multiplicative_expression_nn
	|	additive_expression PLUS multiplicative_expression
	|	additive_expression MINUS multiplicative_expression
	;
shift_expression_nn ::=
		additive_expression_nn
	|	shift_expression LSHIFT additive_expression
	|	shift_expression RSHIFT additive_expression
	|	shift_expression URSHIFT additive_expression
	;
relational_expression_nn ::=
		shift_expression_nn
	|	relational_expression LT shift_expression
	|	relational_expression GT shift_expression
	|	relational_expression LTEQ shift_expression
	|	relational_expression GTEQ shift_expression
	;
instanceof_expression_nn ::=
		relational_expression_nn
	|	instanceof_expression INSTANCEOF reference_type
	;
equality_expression_nn ::=
		instanceof_expression_nn
	|	equality_expression EQEQ instanceof_expression
	|	equality_expression NOTEQ instanceof_expression
	;
and_expression_nn ::=
		equality_expression_nn
	|	and_expression AND equality_expression
	;
exclusive_or_expression_nn ::=
		and_expression_nn
	|	exclusive_or_expression XOR and_expression
	;
inclusive_or_expression_nn ::=
		exclusive_or_expression_nn
	|	inclusive_or_expression OR exclusive_or_expression
	;
conditional_and_expression_nn ::=
		inclusive_or_expression_nn
	|	conditional_and_expression ANDAND inclusive_or_expression
	;
conditional_or_expression_nn ::=
		conditional_and_expression_nn
	|	conditional_or_expression OROR conditional_and_expression
	;
conditional_expression_nn ::=
		conditional_or_expression_nn
	|	conditional_or_expression QUESTION expression 
			COLON conditional_expression
	;
assignment_expression_nn ::=
		conditional_expression_nn
	|	assignment
	;
expression_nn_opt ::=
	|	expression_nn
	;
expression_nn ::=	assignment_expression_nn
	;
constant_expression_nn ::=
		expression_nn
	;
//////////////////////////////////////////////
// the following productions are copied from the standard ones, but
// any right-hand non-terminal has _l appended, and the
// relational_expression_l production reflects that fact that LT
// can get confused with PLT in this (right hand) context.
// The '_l' stands for 'list', since argument_list is the
// only production with the confusing right-hand context.
unary_expression_l ::=
		preincrement_expression_l
	|	predecrement_expression_l
	|	PLUS unary_expression_l
	|	MINUS unary_expression_l
	|	unary_expression_not_plus_minus_l
	;
preincrement_expression_l ::=
		PLUSPLUS unary_expression_l
	;
predecrement_expression_l ::=
		MINUSMINUS unary_expression_l
	;
unary_expression_not_plus_minus_l ::=
		postfix_expression
	|	COMP unary_expression_l
	|	NOT unary_expression_l
	|	cast_expression_l
	;
cast_expression_l ::=
		LPAREN primitive_type dims_opt RPAREN unary_expression_l
	|	LPAREN name RPAREN unary_expression_not_plus_minus_l
	|	LPAREN name PLT reference_type_list_1 RPAREN unary_expression_not_plus_minus_l
	|	LPAREN name dims RPAREN unary_expression_not_plus_minus_l
	|	LPAREN name PLT reference_type_list_1 dims RPAREN unary_expression_not_plus_minus_l
	;
multiplicative_expression_l ::=
		unary_expression_l
	|	multiplicative_expression MULT unary_expression_l
	|	multiplicative_expression DIV unary_expression_l
	|	multiplicative_expression MOD unary_expression_l
	;
additive_expression_l ::=
		multiplicative_expression_l
	|	additive_expression PLUS multiplicative_expression_l
	|	additive_expression MINUS multiplicative_expression_l
	;
shift_expression_l ::=
		additive_expression_l
	|	shift_expression LSHIFT additive_expression_l
	|	shift_expression RSHIFT additive_expression_l
	|	shift_expression URSHIFT additive_expression_l
	;
relational_expression_l ::=
		shift_expression_l
	|	relational_expression LT shift_expression_l
// this is where the _l forms differ: we can possibly confuse an LT
// for a PLT in this context.
	|	relational_expression PLT shift_expression_l
	|	relational_expression GT shift_expression_l
	|	relational_expression LTEQ shift_expression_l
	|	relational_expression GTEQ shift_expression_l
	;
instanceof_expression_l ::=
		relational_expression_l
	|	instanceof_expression INSTANCEOF reference_type
	;
equality_expression_l ::=
		instanceof_expression_l
	|	equality_expression EQEQ instanceof_expression_l
	|	equality_expression NOTEQ instanceof_expression_l
	;
and_expression_l ::=
		equality_expression_l
	|	and_expression AND equality_expression_l
	;
exclusive_or_expression_l ::=
		and_expression_l
	|	exclusive_or_expression XOR and_expression_l
	;
inclusive_or_expression_l ::=
		exclusive_or_expression_l
	|	inclusive_or_expression OR exclusive_or_expression_l
	;
conditional_and_expression_l ::=
		inclusive_or_expression_l
	|	conditional_and_expression ANDAND inclusive_or_expression_l
	;
conditional_or_expression_l ::=
		conditional_and_expression_l
	|	conditional_or_expression OROR conditional_and_expression_l
	;
conditional_expression_l ::=
		conditional_or_expression_l
	|	conditional_or_expression QUESTION expression 
			COLON conditional_expression_l
	;
assignment_expression_l ::=
		conditional_expression_l
	|	assignment_l
	;
assignment_l ::=
		name         assignment_operator assignment_expression_l
	|	field_access assignment_operator assignment_expression_l
	|	array_access assignment_operator assignment_expression_l
	;
expression_l ::=	assignment_expression_l
	;
statement_expression_l ::=
		assignment_l
	|	preincrement_expression_l
	|	predecrement_expression_l
	|	postincrement_expression
	|	postdecrement_expression
	|	method_invocation
	|	class_instance_creation_expression
	;
